apply plugin: 'groovy'

sourceCompatibility = 1.7

version = '0.1.0'

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.3.6'
    compile 'org.slick2d:slick2d-core:1.0.1'

    testCompile group: 'junit', name: 'junit', version: '4.11'
}

// Make natives work
// Copied from solution provided by Szczepan Faber on
// http://forums.gradle.org/gradle/topics/how_to_use_lwjgl_or_how_to_use_native_libraries
def platforms = ['windows', 'linux', 'osx']
platforms.each { platform ->
    task "${platform}Natives" {
        def outputDir = "lib/natives-$platform"
        inputs.files(configurations.compile)
        outputs.dir(outputDir)
        doLast {
            copy {
                def artifacts = configurations.compile
                        .resolvedConfiguration.resolvedArtifacts
                        .findAll { it.classifier == "natives-$platform" }
                artifacts.each {
                    from zipTree(it.file)
                }
                into outputDir
            }
        }
    }
}
